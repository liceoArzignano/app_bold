apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    //noinspection GroovyMissingReturnStatement
    defaultConfig {
        //noinspection GrDeprecatedAPIUsage
        generatedDensities = []
        applicationId 'it.liceoarzignano.bold'
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.16"
        vectorDrawables.useSupportLibrary true
        resConfigs "en", "it"

        ndk {
            moduleName = 'safe-addon-jni'
        }
    }

    lintOptions.disable 'MissingTranslation'

    aaptOptions.additionalParameters "--no-version-vectors"

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'x86', 'arm64-v8a'
            universalApk true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':safe_mod')
    // android support
    compile "com.android.support:appcompat-v7:${androidSupportVersion}"
    compile "com.android.support:design:${androidSupportVersion}"
    compile "com.android.support:cardview-v7:${androidSupportVersion}"
    compile "com.android.support:customtabs:${androidSupportVersion}"
    compile "com.android.support:preference-v7:${androidSupportVersion}"
    compile "com.android.support:preference-v14:${androidSupportVersion}"
    // google + fireBase
    compile "com.google.android.gms:play-services-drive:${googlePlayServicesVersion}"
    compile "com.google.android.gms:play-services-safetynet:${googlePlayServicesVersion}"
    compile "com.google.firebase:firebase-core:${googlePlayServicesVersion}"
    compile "com.google.firebase:firebase-messaging:${googlePlayServicesVersion}"
    compile "com.google.firebase:firebase-crash:${googlePlayServicesVersion}"
    // others
    compile 'io.realm:android-adapters:1.3.0'
    compile 'uk.co.samuelwall:material-tap-target-prompt:1.8.3'
    compile 'devs.mulham.horizontalcalendar:horizontalcalendar:1.0.0'
    compile('com.afollestad.material-dialogs:core:0.9.2.3') { transitive = true }
}

import com.android.build.OutputFile

ext.versionCodes = ['armeabi-v7a':1, x86:2, 'arm64-v8a':3]

android.applicationVariants.all { variant ->
    // assign different version code for each output
    variant.outputs.each { output ->
        int abiVersionCode = project.ext.versionCodes.get(output.getFilter(OutputFile.ABI)) ?: 0
        output.versionCodeOverride = (abiVersionCode * 1000) + android.defaultConfig.versionCode
    }
}

apply plugin: 'com.google.gms.google-services'